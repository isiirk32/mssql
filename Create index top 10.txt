-- Имя почтового профиля, для отправки электонной почты									
DECLARE @profilename AS NVARCHAR(100) = 'monitoring'
-- Получатели сообщений электронной почты, разделенные знаком ";"				
DECLARE @recipients AS NVARCHAR(500) = 'xxx@mail.com'
-- имя базы данных
DECLARE @database NVARCHAR = 'tl-main'
DECLARE @SQL NVARCHAR(max)
DECLARE @finalmassage VARCHAR(max) = ''
DECLARE @subject VARCHAR(200) = ''
DECLARE @not BIT = 0

DECLARE DBcursor CURSOR
FOR
(
		SELECT TOP 10 [SQL] = N'CREATE INDEX [isi_IX_' + CAST(mid.object_id AS VARCHAR) + '_' + CAST(mid.index_handle AS NVARCHAR) + '] ON ' + mid.statement + ' (' + ISNULL(mid.equality_columns, '') + (
				CASE 
					WHEN mid.equality_columns IS NOT NULL
						AND mid.inequality_columns IS NOT NULL
						THEN ', '
					ELSE ''
					END
				) + (
				CASE 
					WHEN mid.inequality_columns IS NOT NULL
						THEN + mid.inequality_columns
					ELSE ''
					END
				) + ')' + (
				CASE 
					WHEN mid.included_columns IS NOT NULL
						THEN ' INCLUDE (' + mid.included_columns + ')'
					ELSE ''
					END
				) + ';'
		FROM sys.dm_db_missing_index_group_stats AS migs
		INNER JOIN sys.dm_db_missing_index_groups AS mig ON (migs.group_handle = mig.index_group_handle)
		INNER JOIN sys.dm_db_missing_index_details AS mid ON (mig.index_handle = mid.index_handle)
		WHERE last_user_seek > DATEADD(DAY, - 1, GETDATE())
			AND user_seeks > 100
			AND DB_NAME(database_id) = @database
			AND avg_total_user_cost * avg_user_impact * (user_seeks + user_scans) > 5000
		)

OPEN DBcursor

FETCH NEXT
FROM DBcursor
INTO @SQL

WHILE @@FETCH_STATUS = 0
BEGIN
	SET @not = 1

	BEGIN TRY
		EXEC sp_executesql @SQL

		SET @finalmassage = @finalmassage + 'Успешно создан индекс: ' + @SQL + CHAR(13) + CHAR(13)
	END TRY

	BEGIN CATCH
		-- Ошбика выполнения операции
		SET @subject = 'СОЗДАНИЕ ИНДЕКСОВ: были ошибки при выполнении операций создания индексов!'
		SET @finalmassage = @finalmassage + 'ОШИБКА создания индекса' + CHAR(13) + CHAR(13) + 'Код ошибки: ' + CAST(ERROR_NUMBER() AS NVARCHAR(10)) + CHAR(13) + CHAR(13) + 'Текст ошибки: ' + ERROR_MESSAGE() + CHAR(13) + CHAR(13) + 'Текст T-SQL: ' + CHAR(13) + @SQL + CHAR(13) + CHAR(13)
	END CATCH;

	FETCH NEXT
	FROM DBcursor
	INTO @SQL
END;

CLOSE DBcursor;

DEALLOCATE DBcursor;

-- Формируем сообщение об успешном или не успешном выполнении операций
IF @not = 0
	SET @subject = 'СОЗДАНИЕ ИНДЕКСОВ: Успешно. Не требуется создание индексов';
ELSE IF @subject = ''
	SET @subject = 'СОЗДАНИЕ ИНДЕКСОВ: Успешное выполнение операции создания индексов';

-- Если задан профиль электронной почты, отправим сообщение
PRINT N'----------------------------------------------------------'

IF @profilename <> ''
	EXEC msdb.dbo.sp_send_dbmail @profile_name = @profilename
		,@recipients = @recipients
		,@body = @finalmassage
		,@subject = @subject;

-- Выводим сообщение о результате
SELECT @subject AS subject
	,@finalmassage AS finalmassage
GO


